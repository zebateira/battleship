class GameEngine
	types
	values
	instance variables
		--houses : set of House;
		public p1: Player;
		public p2: Player;
		public board1own : Board;
		public board1play : Board;
		public board2own : Board;
		public board2play : Board;
		public activePlayer: int := 1;
		public win : bool := false;
		
		inv activePlayer = 1 or activePlayer = 2;
		
	operations
		public GameEngine : int ==> GameEngine
			GameEngine (boardSize) ==
			(
				board1own := new Board(genHouses(boardSize), false);
				board1play := new Board(genHouses(boardSize), true);
				board2own := new Board(genHouses(boardSize), false);
				board2play := new Board(genHouses(boardSize), true);
				p1 := new Player("player1", board1own,board1play);
				p2 := new Player("player2", board2own,board2play);
				IO`print("player ");
				IO`print(startGame());
				IO`print("won! =D");
				
				
			);

		public startGame : () ==> int
			startGame () ==
			(
				while not win do
				(
					if activePlayer = 1 then
						let coords = p1.play() in
						(
							board1play.mark(coords, board2own.hit(coords));
							win := checkVictory(p2.ships);
							if win then return activePlayer;
							activePlayer := 2;
						)
					else
						let coords2 = p2.play() in
						(
							board2play.mark(coords2, board1own.hit(coords2));
							win := checkVictory(p1.ships);
							if win then return activePlayer;
							activePlayer := 1;
						);
				);
				
				return 0;
			);
		
		public checkVictory: seq of Ship ==> bool
			checkVictory (ships) ==
			(
				return forall s in set elems ships & s.isDown();	
			);
			
		public genHouses : int ==> set of House
			genHouses(s) ==
			(
				dcl xx : set of int, yy :set of int, houses : set of House := {};
				xx := {1,...,s};
				yy := {1,...,s};
				
				
				for all x in set xx do
				(
					for all y in set yy do
					(
						houses := houses union {new House(x,y)};
					)
				);
				
				return houses;
			);

	functions
	traces
end GameEngine